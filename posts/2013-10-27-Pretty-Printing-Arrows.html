<!DOCTYPE HTML>
<html><head><title>Functorial Blog - Pretty Printing Arrows</title><link rel="stylesheet" type="text/css" href="http://fonts.googleapis.com/css?family=Roboto+Slab:400,300|Roboto:400,700|Roboto+Condensed:400,700"><link rel="stylesheet" type="text/css" href="http://maxcdn.bootstrapcdn.com/bootstrap/3.3.4/css/bootstrap.min.css"><link rel="stylesheet" type="text/css" href="../assets/default.css"><meta name="viewport" content="width=device-width, initial-scale=1"><script type="text/javascript" src="../assets/gaq.js"></script></head><body><div id="container"><h1 class="text-center">Functorial Blog</h1><p class="lead text-center">A blog about functional programming</p><p class="text-center"><a href="../index.html">Home</a></p></div><section class="dark"><div class="container"><h2>Pretty Printing Arrows</h2><p><small>by Phil Freeman on 2013/10/27</small></p><p>I&#39;d like to show a neat use of arrows for pretty printing an AST.</p>
<pre class="{.haskell}"><code>{-# LANGUAGE GADTs, GeneralizedNewtypeDeriving #-} 

import Data.Maybe (fromMaybe)
import Data.Function (fix)
import qualified Control.Category as C
import Control.Category ((&gt;&gt;&gt;))
import qualified Control.Arrow as A
import Control.Arrow ((***), (&lt;+&gt;))
</code></pre>
<p>Suppose you had defined a type of syntax trees and wanted to write a function to print their representations as code:</p>
<pre class="{.haskell}"><code>data Expr = Var String
          | Abs String Expr
          | App Expr Expr deriving Show
</code></pre>
<h2>A First Attempt</h2>
<p>A first attempt might look something like this:</p>
<pre class="{.haskell}"><code>pretty1 :: Expr -&gt; String
pretty1 (Var v) = v
pretty1 (Abs v e) = &quot;\\&quot; ++ v ++ &quot; -&gt; &quot; ++ pretty1 e
pretty1 (App e1 e2) = &quot;(&quot; ++ pretty1 e1 ++ &quot;) (&quot; ++ pretty1 e2 ++ &quot;)&quot;
</code></pre>
<p>This certainly generates valid code, but the resulting strings tend to contain a lot of redundant parentheses:</p>
<pre><code>ghci&gt; let s = Abs &quot;x&quot; $ 
                Abs &quot;y&quot; $ 
                  Abs &quot;z&quot; $ 
                    App (App (Var &quot;x&quot;) (Var &quot;z&quot;)) 
                          (App (Var &quot;y&quot;) (Var &quot;z&quot;))
                          
ghci&gt; pretty1 s
&quot;\\x -&gt; \\y -&gt; \\z -&gt; ((x) (z)) ((y) (z))&quot;
</code></pre>
<h2>A Better Approach</h2>
<p>Another approach is to thread the current precedence level as an argument, and to parenthesize as a last resort:</p>
<pre class="{.haskell}"><code>type Precedence = Int

pretty2 :: Expr -&gt; String
pretty2 = pretty2&#39; 0
  where
  pretty2&#39; :: Precedence -&gt; Expr -&gt; String
  pretty2&#39; _ (Var v) = v
  pretty2&#39; p (Abs v e) | p &lt; 2 = &quot;\\&quot; ++ v ++ &quot; -&gt; &quot; ++ pretty2&#39; 0 e
  pretty2&#39; p (App e1 e2) = pretty2&#39; 1 e1 ++ &quot; &quot; ++ pretty2&#39; p e2
  pretty2&#39; _ e = &quot;(&quot; ++ pretty2&#39; 0 e ++ &quot;)&quot;
</code></pre>
<p>We can verify that this approach generates better code, and that the precedence rules are still respected:</p>
<pre><code>ghci&gt; pretty2 s
&quot;\\x -&gt; \\y -&gt; \\z -&gt; x z (y z)&quot;

ghci&gt; let k = App (Abs &quot;x&quot; $ Var &quot;x&quot;) (Abs &quot;x&quot; $ Var &quot;x&quot;)

ghci&gt; pretty2 k
&quot;(\\x -&gt; x) (\\x -&gt; x)&quot;
</code></pre>
<p>These approaches are fine, but as the complexity of the AST type increases, I find it harder to keep the various precedence relationships in mind.</p>
<h2>First Class Patterns</h2>
<p>Arrows provide a way to express pattern matches as first class values in a simple way, and then to compose those patterns to create full pretty printers.</p>
<p>First class patterns and their use in pretty printing are not new ideas: interested readers might like to take a look at the following papers for more information:</p>
<ul>
<li>&quot;Type-safe pattern combinators (Functional Pearl)&quot; by Morten Rhiger</li>
<li>&quot;Invertible Syntax Descriptions: Unifying Parsing and Pretty Printing&quot; by Tillmann Rendel and Klaus Ostermann</li>
</ul>
<p>However, I think the use of arrows provides a novel way to build up patterns into complex pretty printers.</p>
<p>The really neat thing is that almost all of the required code can be derived using <code>GeneralizedNewtypeDeriving</code>!</p>
<p>Here is the definition of a <code>Pattern</code> as an <code>Arrow</code>. It takes a value of type <code>a</code>, and either matches successfully, returning a value of type <code>b</code>, or fails. Failure is modelled using the Kleisli category for the <code>Maybe</code> monad:</p>
<pre class="{.haskell}"><code>newtype Pattern a b = Pattern { runPattern :: A.Kleisli Maybe a b } 
  deriving (C.Category, A.Arrow, A.ArrowZero, A.ArrowPlus)

pattern :: Pattern a b -&gt; a -&gt; Maybe b
pattern = A.runKleisli . runPattern
</code></pre>
<p>We can derive instances for <code>Category</code>, <code>Arrow</code>, <code>ArrowZero</code>, and <code>ArrowPlus</code>. The intuition here is that <code>Category</code> gives us composition of patterns, i.e. nested patterns, <code>Arrow</code> gives combinators for working with patterns involving tuples, and <code>ArrowZero</code> and <code>ArrowPlus</code> give us a way to deal with failure and backtracking.</p>
<p>Note: there is also an instance for <code>Applicative</code> which gives another way to work with simultaneous patterns, but I won&#39;t write it out here.</p>
<p>One thing we can&#39;t immediately <code>derive</code> is the <code>Functor</code> instance for <code>Pattern</code>, which will come in useful later. Fortunately, it is easy to write by hand</p>
<pre class="{.haskell}"><code>instance Functor (Pattern a) where
  fmap f p = Pattern $ A.Kleisli $ fmap f . pattern p
</code></pre>
<h2>Some Simple Patterns</h2>
<p>Here are some examples of <code>Pattern</code>s</p>
<pre class="{.haskell}"><code>var :: Pattern Expr String
var = Pattern $ A.Kleisli var&#39;
  where var&#39; (Var s) = Just s
        var&#39; _ = Nothing

lam :: Pattern Expr (String, Expr)
lam = Pattern $ A.Kleisli abs&#39;
  where abs&#39; (Abs s e) = Just (s, e)
        abs&#39; _ = Nothing

app :: Pattern Expr (Expr, Expr)
app = Pattern $ A.Kleisli app&#39;
  where app&#39; (App e1 e2) = Just (e1, e2)
        app&#39; _ = Nothing
</code></pre>
<p>I imagine these are the sort of the thing one could write a Template Haskell splice for. They also seem quite similar to Prisms, which might provide another way to write this code.</p>
<h2>Combining Patterns</h2>
<p>Now we can write some combinators in the spirit of <code>Text.Parsec</code> which allow us to build up new patterns from old, and to apply a pattern recursively:</p>
<pre class="{.haskell}"><code>chainl :: Pattern a (a, a) -&gt; (r -&gt; r -&gt; r) -&gt; Pattern a r -&gt; Pattern a r
chainl split f p = fix $ \c -&gt; (split &gt;&gt;&gt; ((c &lt;+&gt; p) *** p) &gt;&gt;&gt; A.arr (uncurry f))

chainr :: Pattern a (a, a) -&gt; (r -&gt; r -&gt; r) -&gt; Pattern a r -&gt; Pattern a r
chainr split f p = fix $ \c -&gt; (split &gt;&gt;&gt; (p *** (c &lt;+&gt; p)) &gt;&gt;&gt; A.arr (uncurry f))

wrap :: Pattern a (s, a) -&gt; (s -&gt; r -&gt; r) -&gt; Pattern a r -&gt; Pattern a r
wrap split f p = fix $ \c -&gt; (split &gt;&gt;&gt; (C.id *** (c &lt;+&gt; p)) &gt;&gt;&gt; A.arr (uncurry f))
</code></pre>
<h2>Precedence Tables</h2>
<p>In fact, we can go one step further and derive a pattern from a precedence table in the manner of <code>Text.Parsec.Expr</code>:</p>
<pre class="{.haskell}"><code>data OperatorTable a r = OperatorTable { runOperatorTable :: [ [Operator a r] ] }

data Operator a r where
  AssocL :: Pattern a (a, a) -&gt; (r -&gt; r -&gt; r) -&gt; Operator a r
  AssocR :: Pattern a (a, a) -&gt; (r -&gt; r -&gt; r) -&gt; Operator a r
  Wrap   :: Pattern a (s, a) -&gt; (s -&gt; r -&gt; r) -&gt; Operator a r

buildPrettyPrinter :: OperatorTable a r -&gt; Pattern a r -&gt; Pattern a r
buildPrettyPrinter table p = foldl (\p&#39; ops -&gt; foldl1 (&lt;+&gt;) (flip map ops $ \op -&gt;
  case op of
    AssocL pat g -&gt; chainl pat g p&#39;
    AssocR pat g -&gt; chainr pat g p&#39;
    Wrap pat g -&gt; wrap pat g p&#39;
  ) &lt;+&gt; p&#39;) p $ runOperatorTable table
</code></pre>
<p>We need one final function, which parenthesizes an expression:</p>
<pre class="{.haskell}"><code>parenthesize :: Pattern a String -&gt; Pattern a String
parenthesize = fmap parens 
  where
  parens s = &#39;(&#39;:s ++ &quot;)&quot;
</code></pre>
<h2>Finally ...</h2>
<p>This gives us the parts we need to express our previous pretty printer as a <code>Pattern</code>:</p>
<pre class="{.haskell}"><code>expr = buildPrettyPrinter ops (var &lt;+&gt; parenthesize expr)
  where 
    ops = OperatorTable
      [ [ AssocL app $ \e1 e2 -&gt; e1 ++ &quot; &quot; ++ e2 ]
      , [ Wrap lam $ \b s -&gt; &quot;\\&quot; ++ b ++ &quot; -&gt; &quot; ++ s ]
      ]
      
pattern3 :: Expr -&gt; String
pattern3 = fromMaybe (error &quot;Incomplete pattern match&quot;) . pattern expr
</code></pre>
<p>Note that, just like when we define parsers using <code>Text.Parsec.Expr</code>, the use of combinators allows us to write code which directly represents the precedence table!</p>
<h2>Example - Integer Expressions and Binary Operations</h2>
<p>Here&#39;s another example, of expressions supporting integer constants and binary operators.</p>
<pre class="{.haskell}"><code>data Eqn = Const Int
         | Bin Eqn Char Eqn deriving Show

con :: Pattern Eqn Int
con = Pattern $ A.Kleisli con&#39;
  where con&#39; (Const n) = Just n
        con&#39; _ = Nothing

bin :: Char -&gt; Pattern Eqn (Eqn, Eqn)
bin c = Pattern $ A.Kleisli bin&#39;
  where bin&#39; (Bin e1 c&#39; e2) | c == c&#39; = Just (e1, e2)
        bin&#39; _ = Nothing

eqn = buildPrettyPrinter ops (fmap show con &lt;+&gt; parenthesize eqn)
  where 
    ops = OperatorTable
      [ [ binOp &#39;*&#39;, binOp &#39;/&#39; ]
      , [ binOp &#39;+&#39;, binOp &#39;-&#39; ]
      ]
    binOp c = AssocL (bin c) $ \e1 e2 -&gt; e1 ++ c : e2
</code></pre>
<p>For yet another (more developed) example, see my current project on GitHub, <a href="http://github.com/paf31/purescript/">here</a>.</p>
</div></section><div class="container"><p class="text-center text-muted"><small>Copyright Phil Freeman 2010-2015</small></p></div></body></html>