<!DOCTYPE HTML>
<html><head><title>Functorial Blog - Haskell On Azure</title><link rel="stylesheet" type="text/css" href="//fonts.googleapis.com/css?family=Roboto+Slab:300"><link rel="stylesheet" type="text/css" href="//fonts.googleapis.com/css?family=Roboto+Mono:300"><link rel="stylesheet" type="text/css" href="../assets/default.css"><meta name="viewport" content="width=device-width, initial-scale=1"><script type="text/javascript" src="../assets/gaq.js"></script></head><body><header><h1>Haskell On Azure</h1><p>by Phil Freeman on 2012/04/29</p><hr></header><main><p>Today, as a change of topic, I&#39;m going to write about getting a Haskell web application to run on Windows Azure.</p>
<p>Azure provides storage services (the table, queue and blob storage services) exposing REST APIs. I recently uploaded my first cabal package tablestorage to HackageDB, which is a simple wrapper for the web methods of the table storage REST service.</p>
<p>I&#39;ve chosen to use a combination of the Happstack web server and blaze-html to build a very simple note-taking web application, but it should be possible to switch out the particular web server easily.</p>
<pre class="{.text}"><code>module Main where

import Data.Maybe
import Control.Monad
import Control.Monad.Trans
import System.Time
import System.Directory
import Happstack.Server
import qualified Text.Blaze.Html4.Strict as H
import qualified Text.Blaze.Html4.Strict.Attributes as A
import Network.TableStorage
import Text.Blaze ((!), toValue)
</code></pre>
<p>Let&#39;s define the account we are going to be using. An account consists of a service endpoint, an account name and the account&#39;s secret key.</p>
<p>For now, we&#39;ll use the development account, so you will need to have the Windows Azure storage emulator running on your development machine.</p>
<pre class="{.text}"><code>account :: Account
account = developmentAccount 
</code></pre>
<p>When it is time to deploy the account to staging/production, we can replace <code>developmentAccount</code> with a call to defaultAccount with the account information copied from the Azure management portal.</p>
<p>The note-taking appplication will have simple features: there will be multiple &quot;notebooks&quot;, the ability to list the most recent notes in a notebook, and the ability to add a new note to a notebook. We will shard the notes as necessary based on the notebook. In the table storage model, this means that the notebook being used will be determined by the Partition Key of the note entities.</p>
<p>We need the ability to generate a new key. We would like note IDs to be approximately sorted by insertion date. In order that more recent notes will be displayed first, we will generate a key based on the current time, subtracting the seconds component of the current time from a large value to reverse the order of the generated keys.</p>
<pre class="{.text}"><code>newId :: IO String
newId = do
  (TOD seconds picos) &lt;- getClockTime
  return $ show (9999999999 - seconds) ++ show picos
</code></pre>
<p>With that, we can implement the code which will add a new note. This will be implemented as a POST method which accepts its parameters as form-encoded data in the request body.</p>
<p>A new key is generated, and then the insertEntity IO action is used to add the newly created note entity to the notes table, along with the author and text properties as string columns.</p>
<p>If this is successful, we perform a redirect-and-get to display the newly added note to the user. If the insert is not successful, then we respond with 500 - Internal Server Error.</p>
<pre class="{.text}"><code>postNote :: ServerPartT IO Response
postNote = do 
  methodM POST
  tmp &lt;- liftIO getTemporaryDirectory 
  decodeBody $ defaultBodyPolicy tmp 0 1000 1000
  text &lt;- look &quot;text&quot;
  author &lt;- look &quot;author&quot;
  partition &lt;- look &quot;partition&quot;
  result &lt;- liftIO $ do
    id &lt;- newId
    let entity = Entity { entityKey = EntityKey { ekPartitionKey = partition, 
                                                  ekRowKey = id }, 
                          entityColumns = [ (&quot;text&quot;, EdmString $ Just text),
                                            (&quot;author&quot;, EdmString $ Just author)] }
    insertEntity account &quot;notes&quot; entity
  case result of
    Left err -&gt; internalServerError $ toResponse err
    Right _ -&gt; seeOther (&quot;?partition=&quot; ++ partition) $ toResponse ()
</code></pre>
<p>To list the recent notes in the selected notebook, we use queryEntities to query the top 10 entities from the notes table, filtering based on the partition key.</p>
<pre class="{.text}"><code>getNotes :: ServerPartT IO Response
getNotes = do
  methodM GET
  partition &lt;- look &quot;partition&quot; `mplus` return &quot;default&quot;
  let query = defaultEntityQuery { eqPageSize = Just 10,
                                   eqFilter = Just $ CompareString &quot;PartitionKey&quot; Equal partition }
  result &lt;- liftIO $ queryEntities account &quot;notes&quot; query
  case result of
    Left err -&gt; internalServerError $ toResponse err
    Right notes -&gt; ok $ setHeader &quot;Content-Type&quot; &quot;text/html&quot; $ toResponse $ root partition notes
</code></pre>
<p>This demonstrates a very basic query, but the tablestorage package supports more complicated queries including multiple filters.</p>
<p>If the query fails, then we return response code 500. If it is successful, then we return a HTML page built using blaze-html. The is the content of the root function:</p>
<pre class="{.text}"><code>root :: String -&gt; [Entity] -&gt; H.Html
root partition notes = H.html $ do
  H.head $ 
    H.title $ H.toHtml &quot;Notes&quot;
  H.body $ do
    H.h1 $ H.toHtml &quot;Add Note&quot;
    H.form ! A.method (toValue &quot;POST&quot;) $ do
      H.input ! A.type_ (toValue &quot;hidden&quot;) ! A.name (toValue &quot;partition&quot;) ! A.value (toValue partition)
      H.div $ do
        H.label ! A.for (toValue &quot;text&quot;) $ H.toHtml &quot;Text: &quot;
        H.input ! A.type_ (toValue &quot;text&quot;) ! A.name (toValue &quot;text&quot;)
      H.div $ do
        H.label ! A.for (toValue &quot;author&quot;) $ H.toHtml &quot;Author: &quot;
        H.input ! A.type_ (toValue &quot;text&quot;) ! A.name (toValue &quot;author&quot;)
      H.div $
        H.input ! A.type_ (toValue &quot;submit&quot;) ! A.value (toValue &quot;Add Note&quot;)
    H.h1 $ H.toHtml &quot;Recent Notes&quot;
    H.ul $ void $ mapM displayNote notes
</code></pre>
<p>The first part of the page contains a form which will allow the user to post a new note. It contains text boxes for the text and author arguments. The partition key is provided as a hidden field on the form, which the user can change by editing the query string.</p>
<p>The last part of the page lists the most recent ten notes in the selected notebook (partition key). Each note is rendered using the displayNote function:</p>
<pre class="{.text}"><code>displayNote :: Entity -&gt; H.Html
displayNote note = fromMaybe (return ()) $ do
  text &lt;- edmString &quot;text&quot; note
  author &lt;- edmString &quot;author&quot; note
  return $ H.li $ do
    H.toHtml &quot;&#39;&quot;
    H.toHtml text
    H.toHtml &quot;&#39;&quot;
    H.i $ do 
      H.toHtml &quot; says &quot;
      H.toHtml author
</code></pre>
<p>Here, we use the <code>edmString</code> helper function to extract a string-valued column from the list of columns returned by the query. This <code>Maybe</code>-valued function returns Just a string if the named column is indeed string valued, and Nothing if the column is missing or has a different type. The tablestorage package provides corresponding functions for the other column types.</p>
<p>Finally, we can create a web application using the <code>getNotes</code> and <code>postNote</code> routes:</p>
<pre class="{.text}"><code>routes :: [ServerPartT IO Response]
routes = [ getNotes, postNote ]
</code></pre>
<p>The <code>main</code> function performs the setup step of creating the notes table if the table does not already exist. The helper function <code>createTableIfNecessary</code> simplifies this check:</p>
<pre class="{.text}"><code>main :: IO ()
main = do 
  result &lt;- createTableIfNecessary account &quot;notes&quot;
  case result of
    Left err -&gt; putStrLn err
    Right _ -&gt; simpleHTTP nullConf $ msum routes
</code></pre>
<p>The web application can be deployed to the development fabric by using the <code>cspack</code> and <code>csrun</code> command line utilities.</p>
<p>We need to add a few basic configuration files. The web server will run in a worker role, which we set up in the ServiceDefinition.csdef file. We need to add a worker role definition, something like the following:</p>
<pre><code>-- &lt;ServiceDefinition name=&quot;Notes&quot; xmlns=&quot;http://schemas.microsoft.com/ServiceHosting/2008/10/ServiceDefinition&quot;&gt;
--   &lt;WorkerRole name=&quot;WebServer&quot; vmsize=&quot;Small&quot;&gt;
--     &lt;Runtime&gt;
--       &lt;EntryPoint&gt;
--         &lt;ProgramEntryPoint commandLine=&quot;Main.exe&quot; setReadyOnProcessStart=&quot;true&quot; /&gt;
--       &lt;/EntryPoint&gt;
--     &lt;/Runtime&gt;
--     &lt;Endpoints&gt;
--       &lt;InputEndpoint name=&quot;happstackEndpoint&quot; protocol=&quot;tcp&quot; port=&quot;80&quot; /&gt;
--     &lt;/Endpoints&gt;
--   &lt;/WorkerRole&gt;
-- &lt;/ServiceDefinition&gt;
</code></pre>
<p>The <code>ServiceConfiguration.*.cfg</code> files should contain a role section with the same worker role name:</p>
<pre><code>-- &lt;Role name=&quot;WebServer&quot;&gt;
--   &lt;ConfigurationSettings /&gt;
--   &lt;Instances count=&quot;1&quot; /&gt;
-- &lt;/Role&gt;
</code></pre>
<p>After the web application is built, the <code>Main.exe</code> executable is placed in the WebServer role subdirectory, and the package can be built for development deployment using</p>
<pre><code>-- cspack /copyOnly ServiceDefinition.csdef
</code></pre>
<p>The package can then be deployed locally using</p>
<pre><code>-- csrun Notes.csx ServiceConfiguration.Local.cscfg
</code></pre>
<p>You should be able to add new notes, display existing notes, and switch notebooks by editing the query string.</p>
<p>When you are ready to deploy to Azure in staging or production (you will need to replace the development storage account in the code with the account credentials copied from the management portal), you can build a cspkg file for upload to Azure with</p>
<pre><code>-- cspack ServiceDefinition.csdef
</code></pre>
</main><footer><hr><p class="text-muted"><small>Â© Phil Freeman 2010-2021</small></p></footer></body></html>