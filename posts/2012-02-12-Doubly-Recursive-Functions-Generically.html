<!DOCTYPE HTML>
<html><head><title>Functorial Blog - Doubly-Recursive Functions, Generically</title><link rel="stylesheet" type="text/css" href="//fonts.googleapis.com/css?family=Roboto+Slab:300"><link rel="stylesheet" type="text/css" href="//fonts.googleapis.com/css?family=Roboto+Mono:300"><link rel="stylesheet" type="text/css" href="../assets/default.css"><meta name="viewport" content="width=device-width, initial-scale=1"><script type="text/javascript" src="../assets/gaq.js"></script></head><body><header><h1>Doubly-Recursive Functions, Generically</h1><p>by Phil Freeman on 2012/02/12</p><hr></header><main><p>Consider the following recursive function definitions. What do they have in common?</p>
<pre class="{.text}"><code>-- equals 0 0 = True
-- equals 0 n = False
-- equals n 0 = False
-- equals n m = equals (n - 1) (n - 2)

-- unify (Unknown n) (Unknown m) = [(n, m)]
-- unify (Arrow x y) (Arrow w z) = (unify x w) ++ (unify y z)
-- unify _ _ = error &quot;Cannot unify&quot;

-- elementAt 0 (x:xs) = x
-- elementAt n (x:xs) = elementAt (n - 1) xs
-- elementAt _ _ = error &quot;Index out of bounds&quot;
</code></pre>
<p>Well, all three functions take two arguments, and each one is defined recursively. Just as we can define (cata/ana)morphisms for arbitrary base functors, we can unify these three functions under a single abstraction which works over a pair of functors.</p>
<pre class="{.text}"><code>{-# LANGUAGE Rank2Types, DeriveFunctor #-}
</code></pre>
<p>Let&#39;s define a fixed point type for an arbitrary functor f as follows:</p>
<pre class="{.text}"><code>newtype Rec f = In { out :: f (Rec f) }
</code></pre>
<p>We can define folds and unfolds for a functor f in a uniform way as usual.</p>
<pre class="{.text}"><code>fold phi = phi . fmap (fold phi) . out
unfold psi = In . fmap (unfold psi) . psi
</code></pre>
<p>Now look at the definition of the function equals above. Consider the points where the function calls itself recursively. This happens only in the last case, where both sides contribute a smaller term. In general, we can only call a doubly recursive function recursively if we have two arguments to call the function with.</p>
<p>This observation motivates the following definition. For functors <code>f</code> and <code>g</code>, define the following combination <code>f # g</code> of the two:</p>
<pre><code>-- (f # g) x = exists a b. (a -&gt; b -&gt; x) (f a) (g b)
</code></pre>
<p>Now, type <code>x</code> appears in <code>(f # g) x</code> in those places where there is an <code>a</code> contributed from <code>f</code>, and a <code>b</code> contributed from <code>g</code>.</p>
<p>For example, if we define a base functor for the type of natural numbers:</p>
<pre class="{.text}"><code>data Nat t = Zero | Succ t deriving (Show, Functor)
</code></pre>
<p>then we have</p>
<pre><code>-- (Nat # Nat) t = exists a b. (a -&gt; b -&gt; x) (Nat a) (Nat b)
--               ~ exists a b. (a -&gt; b -&gt; x) (1 + a) (1 + b)
--               ~ exists a b. (a -&gt; b -&gt; x) (1 + a + b + a . b)
--               ~ 1 + 1 + 1 + x
</code></pre>
<p>since we cannot eliminate the summands <code>a</code> and <code>b</code> from the existential using only the function of type <code>a -&gt; b -&gt; x</code>.</p>
<p>Let&#39;s name the three unit constructors here <code>ZZ</code>, <code>ZS</code> and <code>SZ</code> depending on the two multiplicands from which they originated in the above isomorphism.</p>
<p>Now we can define a function equals&#39;:</p>
<pre><code>-- equals&#39; :: (Nat # Nat) t -&gt; t
-- equals&#39; ZZ = True
-- equals&#39; ZS = False
-- equals&#39; SZ = False
-- equals&#39; SS t = t
</code></pre>
<p>This looks a lot like the definition of <code>equals</code> given at the top of the article.</p>
<p>With this motivating case out of the way, we now want to use a function of type <code>(f # g) t -&gt; t</code> to define a doubly recursive function over arbitrary functors <code>f</code> and <code>g</code>:</p>
<pre><code>-- (f # g) t -&gt; t ~ (exists a b. (a -&gt; b -&gt; x) (f a) (g b)) -&gt; t
--                ~ (forall a b. (a -&gt; b -&gt; x) -&gt; f a -&gt; g b -&gt; t)
</code></pre>
<p>This is the type of algebra that we will use to fold a pair of structures in parallel.</p>
<p>Finally, let&#39;s define the parallel fold function:</p>
<pre class="{.text}"><code>parFold :: (forall a b. (a -&gt; b -&gt; x) -&gt; f a -&gt; g b -&gt; x) -&gt; Rec f -&gt; Rec g -&gt; x
parFold phi x y = (phi $ parFold phi) (out x) (out y)
</code></pre>
<p>The definition is quite simple: we unwrap each of the arguments by one level, and combine them by using the algebra function, passing the fold itself to the algebra function, so that it may call the fold recursively for smaller arguments. This looks a lot like Mendler style recursion, but using two arguments in parallel.</p>
<p>While we&#39;re at it, let&#39;s define a parallel unfold function, which is dual to the definition above:</p>
<pre class="{.text}"><code>parUnfold :: (forall a b. (x -&gt; (a, b)) -&gt; x -&gt; (f a, g b)) -&gt; x -&gt; (Rec f, Rec g)
parUnfold psi x = let (a, b) = (psi $ parUnfold psi) x in (In a, In b)
</code></pre>
<p>Now we can define the <code>equals</code> and <code>unify</code> functions above as instances of <code>parFold</code>:</p>
<pre class="{.text}"><code>equals = parFold equals&#39; where
  equals&#39; _ Zero Zero = True
  equals&#39; _ Zero _ = False
  equals&#39; _ _ Zero = False
  equals&#39; f (Succ x) (Succ y) = f x y

data Ty t = Unknown Int | Arrow t t deriving (Show, Functor)

unify = parFold unify&#39; where
  unify&#39; _ (Unknown n) (Unknown m) = Just [(n, m)]
  unify&#39; f (Arrow x y) (Arrow z w) = do cs1 &lt;- (f x z); cs2 &lt;- (f y w); return $ cs1 ++ cs2
  unify&#39; _ _ _ = Nothing
</code></pre>
<p>The third example shows that the two recursive structures do not have to be defined over the same base functor:</p>
<pre class="{.text}"><code>data List a t = Empty | Cons a t deriving (Show, Functor)

elementAt = parFold elementAt&#39; where
  elementAt&#39; _ Zero (Cons a t) = Just a
  elementAt&#39; f (Succ n) (Cons a t) = f n t
  elementAt&#39; _ _ _ = Nothing
</code></pre>
</main><footer><hr><p class="text-muted"><small>Â© Phil Freeman 2010-2018</small></p></footer></body></html>