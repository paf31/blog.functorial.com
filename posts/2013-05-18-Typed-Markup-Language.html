<!DOCTYPE HTML>
<html><head><title>Functorial Blog - A Typed Markup Language Based On Haskell</title><link rel="stylesheet" type="text/css" href="//fonts.googleapis.com/css?family=Roboto+Slab:300"><link rel="stylesheet" type="text/css" href="//fonts.googleapis.com/css?family=Roboto+Mono:300"><link rel="stylesheet" type="text/css" href="../assets/default.css"><meta name="viewport" content="width=device-width, initial-scale=1"><script type="text/javascript" src="../assets/gaq.js"></script></head><body><header><h1>A Typed Markup Language Based On Haskell</h1><p>by Phil Freeman on 2013/05/18</p><hr></header><main><p>In this post, I&#39;m going to implement a simple markup language including a parser and type checker. The language&#39;s syntax will be based on Haskell&#39;s syntax, and the type system will look a little bit like System F.</p>
<p>Why am I interested in implementing such a markup language? Well, markup languages like JSON are good at describing the values in a dataset, but not their types, and XML extends this by allowing you to specify a schema, which corresponds to a set of typing rules. Languages like XAML allow you to extend this idea even futher by using an actual type system as a sort of global schema, and then extending the expressiveness of the language with things like subclass polymorphism.</p>
<p>In my opinion, XAML is great for quickly bootstrapping a DSL without having to worry about parsing, but the lack of generics makes it impossible to encode some simple constraints in the schema. I often find myself wanting a markup language which looks like Haskell and which uses similar typing rules.</p>
<p>The simplicity of the language below allows for a very simple implementation: typing is a simple bottom-up pass over a term.</p>
<p>With that, let&#39;s include some modules:</p>
<pre class="{.text}"><code>module Markup where

import Text.Parsec
import qualified Text.Parsec.Token as P
import qualified Text.Parsec.Language as L
import qualified Text.Parsec.Expr as E
import Data.Char
import Data.List (groupBy, nub, (\\))
import Data.Function (on)
import Control.Arrow ((***))
import Control.Applicative ((&lt;$&gt;), (&lt;*&gt;))
import Control.Monad (guard, zipWithM)
import qualified Control.Monad.State as S
</code></pre>
<h2>Language Definition and Examples</h2>
<p>I would like separate languages for type declarations and values. Here are some examples of the sorts of things I would like to be able to write (note, not all of these are expected to pass the typechecker):</p>
<pre class="{.text}"><code>exampleSchema = unlines 
  [ &quot;Nil :: List Z a;&quot;
  , &quot;Singleton :: a -&gt; List (S Z) a;&quot;
  , &quot;Cons :: a -&gt; List n a -&gt; List (S n) a;&quot;
  , &quot;Reverse :: List n a -&gt; List n a;&quot;
  , &quot;Zip :: List n a -&gt; List n b -&gt; List n (Pair a b);&quot;
  , &quot;Tip :: Tree Z a;&quot;
  , &quot;Branch :: Tree n a -&gt; a -&gt; Tree n a -&gt; Tree (S n) a&quot; ]
  
example1 = &quot;x = Zip (Cons 1 $ Cons 2 Nil) (Cons 1 Nil)&quot; 
example2 = &quot;x = Zip (Cons 1 $ Reverse $ Cons 2 $ Cons 3 Nil) (Cons \&quot;a\&quot; $ Cons \&quot;b\&quot; $ Singleton \&quot;c\&quot;)&quot; 
example3 = &quot;x = Branch (Branch Tip 1 Tip) 2 (Branch Tip 3 Tip)&quot; 
example4 = &quot;x = Branch (Branch Tip 1 Tip) 2 Tip&quot; 
example5 = &quot;x = Cons (Branch Tip 1 Tip) Nil&quot; 
example6 = &quot;x = 1 `Cons` (2 `Cons` (3 `Cons` Nil))&quot; 
example7 = &quot;x = 1 `Cons` Nil `Zip` (2 `Cons` Nil)&quot; 
example8 = &quot;x = Nil `Cons` Nil `Zip` ((Cons 1 $ Nil) `Cons` Nil)&quot; 
example9 = unlines 
  [ &quot;x = Nil;&quot;
  , &quot;y = Cons x Nil;&quot;
  , &quot;z = Cons y Nil&quot; ]
</code></pre>
<p>Notice that the type definition language supports things like type constructors and phantom types to allow me to encode simple constraints in the type of a declaration. For example, I can enforce the fact that the Zip constructor takes two lists of the same length. Also, there are no higher order functions, since there are no user-defined functions other than constructors, and no data declarations: all types are open for extension.</p>
<p>The value language copies several ideas from Haskell: infix constructors using backticks, and application using both juxtaposition and <code>$</code>. Other than that, the syntax is very simple.</p>
<p>Here are the data types which represent terms and types, along with their pretty printing functions:</p>
<pre class="{.text}"><code>data Tm 
  = TmName String
  | TmAtom String 
  | TmStr String 
  | TmInt Integer 
  | TmApp Tm Tm deriving (Show, Eq)

prettyTm :: Tm -&gt; String
prettyTm (TmName s) = s
prettyTm (TmAtom s) = s
prettyTm (TmStr s) = show s
prettyTm (TmInt n) = show n
prettyTm (TmApp t1 t2@(TmApp _ _)) = prettyTm t1 ++ &quot; (&quot; ++ prettyTm t2 ++ &quot;)&quot;
prettyTm (TmApp t1 t2) = prettyTm t1 ++ &quot; &quot; ++ prettyTm t2
    
data Ty
  = TyCon String
  | TyApp Ty Ty
  | TyArr Ty Ty
  | TyVar String deriving (Show, Eq)
  
prettyTy :: Ty -&gt; String
prettyTy (TyCon s) = s
prettyTy (TyVar v) = v
prettyTy (TyApp t1 t2@(TyCon _)) = prettyTy t1 ++ &quot; &quot; ++ prettyTy t2
prettyTy (TyApp t1 t2) = prettyTy t1 ++ &quot; (&quot; ++ prettyTy t2 ++ &quot;)&quot;
prettyTy (TyArr t1 t2) = prettyTy t1 ++ &quot; -&gt; (&quot; ++ prettyTy t2 ++ &quot;)&quot;
</code></pre>
<h2>Parsing Types and Values Using Parsec</h2>
<p>Now let&#39;s implement a parser. To keep things simple, I&#39;m going to use the lexer generator from <code>Text.Parsec.Token</code>.</p>
<pre class="{.text}"><code>languageDef :: P.LanguageDef st
languageDef = L.haskellStyle 
  { P.opStart         = fail &quot;Operators are not supported&quot;
  , P.opLetter        = fail &quot;Operators are not supported&quot;
  , P.reservedNames   = []
  , P.reservedOpNames = []
  , P.caseSensitive   = True }
  
lexer             = P.makeTokenParser languageDef 
identifier        = P.identifier lexer
whiteSpace        = P.whiteSpace lexer
stringLiteral     = P.stringLiteral lexer 
integer           = P.integer lexer 
symbol            = P.symbol lexer 
lexeme            = P.lexeme lexer 
parens            = P.parens lexer 
semi              = P.semi lexer 

identu :: Parsec String u String
identu = lookAhead upper &gt;&gt; identifier
  
identl :: Parsec String u String
identl = lookAhead lower &gt;&gt; identifier
</code></pre>
<p>There are parsers for terms:</p>
<pre class="{.text}"><code>tmName :: Parsec String u Tm
tmName = lexeme $ identl &gt;&gt;= return . TmName
  
tmAtom :: Parsec String u Tm
tmAtom = lexeme $ identu &gt;&gt;= return . TmAtom

tmStr :: Parsec String u Tm
tmStr = lexeme $ stringLiteral &gt;&gt;= return . TmStr
  
tmInt :: Parsec String u Tm
tmInt = lexeme $ integer &gt;&gt;= return . TmInt

tmBracket :: Parsec String u Tm
tmBracket = lexeme $ parens tm
  
tm :: Parsec String u Tm
tm = E.buildExpressionParser 
     [ [ E.Infix (return TmApp) E.AssocLeft ]
     , [ E.Infix (((.) TmApp . TmApp) &lt;$&gt; between (symbol &quot;`&quot;) (symbol &quot;`&quot;) tmAtom) E.AssocLeft ]
     , [ E.Infix (symbol &quot;$&quot; &gt;&gt; return TmApp) E.AssocRight ]  ]
     (tmBracket &lt;|&gt; tmStr &lt;|&gt; tmInt &lt;|&gt; try tmName &lt;|&gt; tmAtom)
</code></pre>
<p>and parsers for types:</p>
<pre class="{.text}"><code>tyVar :: Parsec String u Ty
tyVar = lexeme $ identl &gt;&gt;= return . TyVar

tyCon :: Parsec String u Ty
tyCon = lexeme $ identu &gt;&gt;= return . TyCon
  
tyBracket :: Parsec String u Ty
tyBracket = parens ty
  
ty :: Parsec String u Ty
ty = E.buildExpressionParser 
     [ [ E.Infix (return TyApp) E.AssocLeft ]
     , [ E.Infix (symbol &quot;-&gt;&quot; &gt;&gt; return TyArr) E.AssocRight ] ]
     (tyBracket &lt;|&gt; try tyVar &lt;|&gt; tyCon)
</code></pre>
<p>The parsers <code>tm</code> and <code>ty</code> are then used to construct top-level parsers <code>schema</code> for type declarations and <code>doc</code> for value declarations:</p>
<pre class="{.text}"><code>valueDecl :: Parsec String u (String, Tm)
valueDecl = do
  name &lt;- identl
  symbol &quot;=&quot;
  t &lt;- tm
  return (name, t)

doc :: Parsec String u [(String, Tm)]
doc = do
  whiteSpace 
  ds &lt;- sepBy valueDecl (lexeme semi)
  eof
  return ds

typeDecl :: Parsec String u (String, Ty)
typeDecl = do
  name &lt;- identu
  symbol &quot;::&quot;
  t &lt;- ty
  return (name, t)

schema :: Parsec String u [(String, Ty)]
schema = do
  whiteSpace 
  ds &lt;- sepBy typeDecl (lexeme semi)
  eof
  return ds
</code></pre>
<p>There is quite a bit going on here, but for the most part, the parser is assembled from reusable pieces in <code>Text.Parsec.Token</code> and some utility functions such as <code>sepBy</code>. The parser serves as a fairly good definition of the language syntax: the <code>tm</code> and <code>ty</code> parsers are just constructed using <code>buildExpressionParser</code> from the precedence tables for the term and type constructors respectively.</p>
<h2>Implementing a Typechecker</h2>
<p>The next step is to write the typechecker. This is actually quite simple since there are no function types other than constructors, and all quantification over free type variables implicitly happens at the top level.</p>
<p>The first step is to implement a unification function. This is very easy: types are either equal modulo variable substitution, or they do not unify at all:</p>
<pre class="{.text}"><code>unify :: Ty -&gt; Ty -&gt; Either String [(String, Ty)]
unify (TyVar s) (TyVar t) = return []
unify (TyVar s) t = return [(s, t)]
unify t (TyVar s) = return [(s, t)]
unify (TyCon ct1) (TyCon ct2) 
  | ct1 == ct2 = return []
unify (TyApp ty1 ty2) (TyApp ty3 ty4) = (++) &lt;$&gt; unify ty1 ty3 &lt;*&gt; unify ty2 ty4
unify (TyArr ty1 ty2) (TyArr ty3 ty4) = (++) &lt;$&gt; unify ty1 ty3 &lt;*&gt; unify ty2 ty4
unify t1 t2 = Left $ &quot;Cannot unify &quot; ++ prettyTy t1 ++ &quot; with &quot; ++ prettyTy t2
</code></pre>
<p>Type variable substitution is also a simple pattern match:</p>
<pre class="{.text}"><code>subst :: [(String, Ty)] -&gt; Ty -&gt; Ty
subst vs v@(TyVar s) = maybe v id $ lookup s vs
subst vs (TyApp t1 t2) = TyApp (subst vs t1) (subst vs t2)
subst vs (TyArr t1 t2) = TyArr (subst vs t1) (subst vs t2)
subst _ t = t
</code></pre>
<p>One important function which is used in the typechecker is <code>specialize</code>, which is used to apply a polymorphic function to an argument. The idea is that <code>specialize f x</code> should return the most general type of <code>f x</code>. The helper function <code>isFunction</code> is used to ensure that each type variable was unified with at most one type.</p>
<pre class="{.text}"><code>isFunction :: (Eq dom, Eq cod) =&gt; [(dom, cod)] -&gt; Bool
isFunction = all ((== 1) . length . nub . map snd) . groupBy ((==) `on` fst)
  
specialize :: Ty -&gt; Ty -&gt; Either String Ty
specialize (TyArr t1 t2) t3 = do
  vs &lt;- unify t1 t3
  guard $ isFunction vs
  return $ subst vs t2
specialize t _ = Left $ &quot;Expected a function, found &quot; ++ prettyTy t
</code></pre>
<p>We need one more helper function, which is used to rename free variables in a polymorphic type. This is used to make sure type variables do not overlap when a function application gets typed.</p>
<p>The <code>renameAll</code> function gives new names to all of the free type variables appearing in a type, using an association list to store the currently-generated names:</p>
<pre class="{.text}"><code>renameAll :: (Eq a) =&gt; a -&gt; Ty -&gt; S.State [((String, a), String)] Ty
renameAll _ t@(TyCon _) = return t
renameAll key (TyVar v) = do
  m &lt;- S.get
  case lookup (v, key) m of
    Nothing -&gt; do
      let name = unusedName (map snd m)
      S.modify $ (:) ((v, key), name)
      return $ TyVar name
    Just name -&gt; return $ TyVar name
renameAll key (TyApp t1 t2) = TyApp &lt;$&gt; renameAll key t1 &lt;*&gt; renameAll key t2
renameAll key (TyArr t1 t2) = TyArr &lt;$&gt; renameAll key t1 &lt;*&gt; renameAll key t2

unusedName :: [String] -&gt; String
unusedName used = head $ map ((&quot;t&quot; ++) . show) [0..] \\ used
</code></pre>
<p>We can now write the typechecker. It is a simple application of the <code>specialize</code> and <code>renameAll</code> helper functions:</p>
<pre class="{.text}"><code>typeOf :: [(String, Ty)] -&gt; Tm -&gt; Either String Ty
typeOf ctx (TmName s) = maybe (Left $ &quot;Unknown name &quot; ++ s) Right $ lookup s ctx
typeOf ctx (TmAtom s) = maybe (Left $ &quot;Unknown constructor &quot; ++ s) Right $ lookup s ctx
typeOf _ (TmInt _) = return (TyCon &quot;Int&quot;)
typeOf _ (TmStr _) = return (TyCon &quot;String&quot;)
typeOf ctx (TmApp t1 t2) = do
  tyArg &lt;- typeOf ctx t2
  tyFun &lt;- typeOf ctx t1
  flip S.evalState [] $ do 
    tyFun&#39; &lt;- renameAll True tyFun
    tyArg&#39; &lt;- renameAll False tyArg
    return $ specialize tyFun&#39; tyArg&#39;
  
typesOf :: [(String, Ty)] -&gt; [(String, Tm)] -&gt; Either String [(String, Ty)]
typesOf _ [] = return []
typesOf ctx ((name, tm):tms) = do
  ty &lt;- typeOf  ctx tm
  let first = (name, ty)
  rest &lt;- typesOf (first:ctx) tms
  return (first:rest)
</code></pre>
<p>Let&#39;s test the typechecker and parser on some of the examples at the top of the post:</p>
<pre class="{.text}"><code>parseErr p = either (Left . show) Right . parse p &quot;&quot;
  
test sch val = do
  s &lt;- parseErr schema sch
  d &lt;- parseErr doc val
  tys &lt;- typesOf s d
  return $ map (id *** prettyTy) tys
</code></pre>
<pre><code>ghci&gt; test exampleSchema example1
Left &quot;Cannot unify S Z with Z&quot;
ghci&gt; test exampleSchema example2
Right [(&quot;x&quot;,&quot;List (S (S (S Z))) (Pair Int String)&quot;)]
ghci&gt; test exampleSchema example3
Right [(&quot;x&quot;,&quot;Tree (S (S Z)) Int&quot;)]
</code></pre>
<p>This is good - the ill-typed expressions returned a typing error, and the well-typed expressions returned the correct types, including the correctly inferred placeholders for phantom types.</p>
<p>There are a few other small additions to the language that I would like to make such as infix operators, kind checking, better error messages and support for other primitive types.</p>
<h2>A Mini Language For Relational Queries</h2>
<p>As a more useful example, I&#39;ve put together a minimal DSL for partially typed relational queries. The type system is used to make sure pairs of columns equated in joins have the same type, and to track the tables used in a query. Try replacing some of the constructors to see the ways in which the type system is used!</p>
<pre class="{.text}"><code>relationSchema = unlines 
  [ &quot;UserID         :: Column User PK;&quot;
  , &quot;UserName       :: Column User String;&quot;
  , &quot;UserCompanyID  :: Column User PK;&quot;
  , &quot;CompanyID      :: Column Company PK;&quot;
  , &quot;CompanyName    :: Column Company String;&quot;
  , &quot;Table          :: HList (Column t) -&gt; Query (Table t);&quot;
  , &quot;Join           :: Query r1 -&gt; Query r2 -&gt; Expr (Cross r1 r2) Bool -&gt; Query (Cross r1 r2);&quot;
  , &quot;Column         :: Column t ty -&gt; Expr (Table t) ty;&quot;
  , &quot;Left           :: Expr t1 ty -&gt; Expr (Cross t1 t2) ty;&quot;
  , &quot;Right          :: Expr t2 ty -&gt; Expr (Cross t1 t2) ty;&quot;
  , &quot;Eq             :: Expr t ty -&gt; Expr t ty -&gt; Expr t Bool;&quot;
  , &quot;Nil            :: HList f;&quot;
  , &quot;Cons           :: f t -&gt; HList f -&gt; HList f&quot; ]

relationExample = unlines 
  [ &quot;query = Join&quot;
  , &quot;          (Table (Cons UserName $ Cons UserCompanyID Nil))&quot;
  , &quot;          (Table (Cons CompanyID $ Cons CompanyName Nil))&quot;
  , &quot;          ((Left $ Column UserCompanyID) `Eq` (Right $ Column CompanyID))&quot; ]
</code></pre>
<pre><code>ghci&gt; test relationSchema relationExample
Right [(&quot;query&quot;,&quot;Query (Cross (Table User) (Table Company))&quot;)]
</code></pre>
<h2>Implementing a Kind Checker</h2>
<p>Ensuring terms are well-typed is pretty useless if the type language is itself typeless, so let&#39;s implement a kind checker for type definitions in a schema.</p>
<p>The kind checker below is a modified version of a type checker from an earlier post. It works by generating kind constraints by performing a bottom-up traversal of a type, and then solving tose constraints by substitution.</p>
<p>Here is the type of kinds, including placeholders for unknown kinds:</p>
<pre class="{.text}"><code>type Unknown = Int
  
data Kind
  = KindUnknown Unknown
  | KindStar
  | KindArr Kind Kind deriving (Show, Eq)
  
prettyKind :: Kind -&gt; String
prettyKind (KindUnknown n) = &quot;k&quot; ++ show n
prettyKind KindStar = &quot;*&quot;
prettyKind (KindArr k1@(KindArr _ _) k2) = &quot;(&quot; ++ prettyKind k1 ++ &quot;) -&gt; &quot; ++ prettyKind k2
prettyKind (KindArr k1 k2) = prettyKind k1 ++ &quot; -&gt; &quot; ++ prettyKind k2
</code></pre>
<p>I&#39;ll start as usual with some basic utility functions which will be used during kind checking. Two crucial functions are the replacement of an unknown with another kind, and an occurs-check function:</p>
<pre class="{.text}"><code>replace :: Unknown -&gt; Kind -&gt; Kind -&gt; Kind
replace i x u@(KindUnknown j) 
  | i == j = x
  | otherwise = u
replace _ _ KindStar = KindStar
replace i x (KindArr k1 k2) = KindArr (replace i x k1) (replace i x k2)

occursCheck :: Unknown -&gt; Kind -&gt; Either String ()
occursCheck i (KindUnknown j) | i == j = return ()
occursCheck i x = occursCheck&#39; i x where
  occursCheck&#39; i (KindUnknown j) | i == j = fail &quot;Occurs check failed during kind checking&quot;
  occursCheck&#39; i (KindArr k1 k2) = occursCheck&#39; i k1 &gt;&gt; occursCheck&#39; i k2
  occursCheck&#39; _ _ = return ()
</code></pre>
<p>Constraint generation happens by unification. The <code>unify</code> function takes two kinds and generates the constraints imposed by their equality, or returns a unification error.</p>
<pre class="{.text}"><code>type KindConstraint = (Unknown, Kind)

type SolutionSet = Unknown -&gt; Kind

unifyKinds :: Kind -&gt; Kind -&gt; Either String [KindConstraint]
unifyKinds (KindUnknown i) x = return [(i, x)]
unifyKinds x (KindUnknown i) = return [(i, x)]
unifyKinds KindStar KindStar = return  []
unifyKinds (KindArr k1 k2) (KindArr k3 k4) = (++) &lt;$&gt; unifyKinds k1 k3 &lt;*&gt; unifyKinds k2 k4
unifyKinds k1 k2 = fail $ &quot;Cannot unify &quot; ++ show k1 ++ &quot; with &quot; ++ show k2
</code></pre>
<p>The general constraint generation function works bottom-up over the structure of a type:</p>
<pre class="{.text}"><code>generateConstraints :: (Monad m, S.MonadState ([(String, Unknown)], Unknown) m) =&gt; Ty -&gt; m [KindConstraint]
generateConstraints ty = do
  (cs, n) &lt;- generateConstraints&#39; ty
  return $ (n, KindStar) : cs
  where
  generateConstraints&#39; :: (Monad m, S.MonadState ([(String, Unknown)], Unknown) m) =&gt; Ty -&gt; m ([KindConstraint], Unknown)
  generateConstraints&#39; (TyVar v) = do
    (m, _) &lt;- S.get
    case lookup v m of
      Nothing -&gt; do
        n &lt;- newUnknown
        S.modify $ (:) (v, n) *** id
        return ([], n)
      Just unk -&gt; return ([], unk)
  generateConstraints&#39; (TyCon s) = do
    (m, _) &lt;- S.get
    case lookup s m of
      Nothing -&gt; do
        n &lt;- newUnknown
        S.modify $ (:) (s, n) *** id
        return ([], n)
      Just unk -&gt; return ([], unk)
  generateConstraints&#39; (TyApp ty1 ty2) = do
    (c1, n1) &lt;- generateConstraints&#39; ty1
    (c2, n2) &lt;- generateConstraints&#39; ty2
    thisName &lt;- newUnknown
    let newConstraint = (n1, KindArr (KindUnknown n2) (KindUnknown thisName))
    return $ ((newConstraint:c1) ++ c2, thisName)
    
newUnknown :: (Monad m, S.MonadState (a, Unknown) m) =&gt; m Unknown
newUnknown = S.modify (id *** (+1)) &gt;&gt; S.get &gt;&gt;= return . snd
</code></pre>
<p>Constraints are solved by substitution using <code>replace</code>:</p>
<pre class="{.text}"><code>solve :: [KindConstraint] -&gt; Either String SolutionSet
solve cs = solve&#39; cs KindUnknown where
  solve&#39; [] ss = return ss
  solve&#39; (c@(i, x):cs) ss = do
    occursCheck i x
    cs&#39; &lt;- substConstraints c cs
    let ss&#39; = substSolutionSet c ss
    solve&#39; cs&#39; ss&#39;
  substConstraints c [] = return []
  substConstraints (i, x) ((j, y): cs) 
    | i == j = (++) &lt;$&gt; substConstraints (i, x) cs &lt;*&gt; unifyKinds x y
    | otherwise = (:) (j, replace i x y) &lt;$&gt; substConstraints (i, x) cs
  substSolutionSet (i, x) = (.) (replace i x)
</code></pre>
<p>Now we can write the kind checker. The <code>applications</code> function splits an arrow type into parts, so that kind checking can work on each part.</p>
<pre class="{.text}"><code>applications :: Ty -&gt; [Ty]
applications t = applications&#39; t [] where
  applications&#39; (TyArr t1 t2) = applications&#39; t1 . applications&#39; t2
  applications&#39; t = (:) t

kindCheck :: [Ty] -&gt; Either String [(String, Kind)]
kindCheck = uncurry kindCheck&#39; 
  . (solve *** id) 
  . (concat *** fst) 
  . flip S.runState ([], 0) 
  . flip forM generateConstraints 
  . concatMap applications
  where
  kindCheck&#39; :: Either String SolutionSet -&gt; [(String, Unknown)] -&gt; Either String [(String, Kind)]
  kindCheck&#39; ss unks = do
    ss&#39; &lt;- ss
    return $ map (id *** ss&#39;) unks
</code></pre>
<p>Let&#39;s try it out.</p>
<pre><code>ghci&gt; parseErr schema exampleSchema &gt;&gt;= fmap (map (id *** prettyKind)) . kindCheck . map snd
Right [
  (&quot;Tree&quot;, &quot;k10 -&gt; * -&gt; *&quot;),
  (&quot;Pair&quot;, &quot;* -&gt; * -&gt; *&quot;),
  (&quot;b&quot;, &quot;*&quot;),
  (&quot;n&quot;, &quot;k10&quot;),
  (&quot;S&quot;, &quot;k10 -&gt; k10&quot;),
  (&quot;a&quot;, &quot;*&quot;),
  (&quot;Z&quot;, &quot;k10&quot;),
  (&quot;List&quot;, &quot;k10 -&gt; * -&gt; *&quot;) ]
</code></pre>
</main><footer><hr><p class="text-muted"><small>Â© Phil Freeman 2010-2018</small></p></footer></body></html>