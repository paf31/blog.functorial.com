<!DOCTYPE HTML>
<html><head><title>Functorial Blog - Teaching Haskell To Juggle</title><link rel="stylesheet" type="text/css" href="http://fonts.googleapis.com/css?family=Roboto+Slab:300"><link rel="stylesheet" type="text/css" href="http://fonts.googleapis.com/css?family=Roboto+Mono:300"><link rel="stylesheet" type="text/css" href="../assets/default.css"><meta name="viewport" content="width=device-width, initial-scale=1"><script type="text/javascript" src="../assets/gaq.js"></script></head><body><header><h1>Teaching Haskell To Juggle</h1><p>by Phil Freeman on 2013/05/25</p><hr></header><main><p><link rel="stylesheet" href="../assets/juggling.css" type="text/css"></link></p>
<p><script src="../assets/jquery.min.js" type="text/javascript"></script></p>
<p><script src="../assets/juggling.min.js" type="text/javascript"></script></p>
<p>While recently learning some new juggling tricks, I decided to write a few Haskell functions to find valid patterns.</p>
<p>Siteswaps are a method of describing the throws involved in various juggling patterns. I&#39;m only interested here in so-called <em>vanilla</em> siteswaps, where a single ball is thrown from each hand, and hands take turns to throw balls. I&#39;ll give a very basic overview of vanilla siteswaps and associated ideas, before showing the Haskell code. Interested readers are encouraged to survey the online literature covering the theory of siteswaps, which is extensive.</p>
<p>The numbers in a siteswap pattern represent the heights to which the various balls are thrown. Odd numbers indicate throws to the opposite hand, and even numbers indicate throws to the same hand. The larger a number, the higher the throw, and the longer the ball will be in the air.</p>
<p>For example, the siteswap pattern <code>33</code> represents the three ball cascade. We can read it as follows: the first hand throws a ball to height 3 into the second hand on beat 1, followed by the second hand throwing a ball to height 3 into the first hand on beat 2. After this, the pattern repeats. The first ball will land in the second hand on beat 4, three beats after it was thrown.</p>
<p>The siteswap pattern <code>441</code> represents a 3-ball pattern which can be inserted as a trick into the regular 3-ball cascade. The notation indicates that the trick consists of a self-throw from each hand to height 4 on beats 1 and 2, followed by the first hand throwing a ball quickly to the second hand on beat 3.</p>
<div class="j" j="[4,4,1]" style="float: left;"></div>
<div class="j" j="[3,3,3,3,3,3,4,4,1]" style="float: left;"></div>
<div style="clear: left;"></div>
<p>The 3-ball pattern <code>441</code> (left), and the same pattern inserted into the 3-ball cascade (right).</p>
<p>The number of props used in a pattern is given by the average of the numbers in the pattern. We can see therefore that the 3-ball cascade does indeed use 3 props, as does <code>441</code>.</p>
<p>Not every sequence of integer values constitutes a valid siteswap pattern. We need to make sure that on each beat, at most one ball lands in the appropriate hand. For example, if we know that on a given beat, a ball is already scheduled to land in the opposite hand on the next beat, then we cannot throw a 1, since that will result in a collision.</p>
<p>This determines a graph of underlying states and valid transitions, whose paths correspond to the valid siteswap patterns. The Haskell code below will allow us to query this graph, and ask things like &quot;what is the complete list of valid n-ball tricks which can be inserted into a n-ball cascade?&quot;</p>
<p>The nodes of the graph will need to contain enough data to answer whether or not a ball is scheduled to land <code>n</code> beats in the future for all <code>n &lt; N</code> for some <code>N</code>. We could therefore take the type of states to be a list of booleans of length <code>N</code>. However, it is more conventional to pack these booleans as bits into a single integer and to refer to states by this integer, so we will take the type of states to be a (newtype-wrapped) integer. The number of bits is equal to the maximum height to which a ball will be thrown.</p>
<p>For example, the 3-ball cascade stays in state 7 = 2^0^ + 2^1^ + 2^2^ = 111~2~. On each beat <code>n</code>, a ball is scheduled to land on beats <code>n</code>, <code>n+1</code>, and <code>n+2</code>. Throwing a ball to height 3 will schedule a ball to land on beat <code>n+3</code>.</p>
<p>We will represent heights, patterns and states as follows:</p>
<pre class="{.text}"><code>import Data.Bits
import Data.Maybe
import Data.List
import Control.Monad
import Control.Applicative
 
newtype Height = Height { height :: Int } deriving (Show, Eq, Ord)
 
newtype Pattern = Pattern { pattern :: [Height] } deriving (Show, Eq)
 
newtype State = State { state :: Int } deriving (Show, Eq, Ord)
</code></pre>
<p>We can transition to a new state by throwing a ball to a given height. If the first bit of the state is zero, then there is no ball scheduled to land, hence nothing available to throw, and the only valid throw is a fake throw to &quot;height&quot; 0. If the first bit is a one, then we can throw to any height <code>h</code> such that the bit at position <code>h + 1</code> is a zero.</p>
<p>A valid throw is indicated by a return value constructed using <code>Just</code>. An invalid throw returns <code>Nothing</code>.</p>
<pre class="{.text}"><code>
throw :: Height -&gt; State -&gt; Maybe State
throw (Height 0) (State s) | (s .&amp;. 1) == 0 = 
  Just $ State $ s `shiftR` 1
throw (Height h) (State s) | h &gt; 0 &amp;&amp; ((1 `shift` h) .&amp;. s) == 0 = 
  Just $ State $ (s `shiftR` 1) .|. (1 `shift` (h - 1))
throw _ _ = Nothing
</code></pre>
<p>We can also use <code>foldM</code> to attemt to throw a pattern. The return value will be <code>Nothing</code> if any throw was invalid.</p>
<pre class="{.text}"><code>throwMany :: Pattern -&gt; State -&gt; Maybe State
throwMany p s0 = foldM (flip throw) s0 (pattern p)
</code></pre>
<p>To find valid patterns, we need to find cycles in the graph of states and throws.</p>
<p>A graph can be represented by a list of vertices together with a list of edges:</p>
<pre class="{.text}"><code>type Graph v e = ([v], [(v, v, e)])
</code></pre>
<p>Cycles are just paths whose start and end vertices coincide. We can find paths by choosing a start vertex and performing a depth first traversal of the graph, keeping track of the vertices we have already visited:</p>
<pre class="{.text}"><code>paths :: (Eq v) =&gt; Graph v e -&gt; [(v, v, [e])]
paths (vs, es) = 
  [ (v1, v2, p) 
  | v1 &lt;- vs
  , (v2, p) &lt;- paths&#39; v1 es [] ] 
  where
  paths&#39; v es visited = (v, []) : 
    [ (v3, (e:p))
    | (v1, v2, e) &lt;- es
    , v1 == v
    , not $ v2 `elem` visited
    , (v3, p) &lt;- paths&#39; v2 es (v1:visited) ]
</code></pre>
<p>Cycles are then given by filtering the list of paths:</p>
<pre class="{.text}"><code>cycles :: (Eq v) =&gt; Graph v e -&gt; [(v, [e])]
cycles (vs, es) = [ (v1, p ++ [e]) 
                  | (v1, v2, p) &lt;- paths (vs, es)
                  , not $ null p
                  , (v2&#39;, v3, e) &lt;- es
                  , v2 == v2&#39;
                  , v3 == v1 ]
</code></pre>
<p>In order to generate the graph of states and throws, we need to fix a maximum height and a number of balls to throw.</p>
<p>The number of 1 bits in a state represents the number of balls in the air:</p>
<pre class="{.text}"><code>countBits :: Int -&gt; Int
countBits 0 = 0
countBits 1 = 1
countBits n = let (d, r) = n `divMod` 2 in countBits d + countBits r
</code></pre>
<p>The function <code>throw</code> is the transition function which can be used to tell whether or not a throw corresponds to an edge in the graph. Here is a function which generically constructs a graph from such a function:</p>
<pre class="{.text}"><code>generateGraph :: (Eq v) =&gt; (e -&gt; v -&gt; Maybe v) -&gt; [v] -&gt; [e] -&gt; Graph v e
generateGraph transition vs es =
  let edges = [ (v1, v2, e) 
              | v1 &lt;- vs 
              , v2 &lt;- vs
              , e &lt;- es
              , transition e v1 == Just v2] in (vs, edges)
</code></pre>
<p>We can now construct the state transition graph for a number of balls with a maximum throw height by using <code>generateGraph</code> with <code>throw</code>. We can use <code>countBits</code> to construct the set of states given the maximum height <code>maxHeight</code>.</p>
<pre class="{.text}"><code>graph :: Height -&gt; Int -&gt; Graph State Height
graph (Height maxHeight) numBalls = 
  let maxState  = (1 `shiftL` maxHeight) - 1
      states    = map State $ filter ((== numBalls) . countBits) [0..maxState] in
  generateGraph throw states (map Height [0..maxHeight])
</code></pre>
<p>Enumerating valid patterns for a number of balls with a maximum throw height is as simple as finding cycles in this graph:</p>
<pre class="{.text}"><code>validPatterns :: Height -&gt; Int -&gt; [(State, [Height])]
validPatterns maxHeight numBalls = cycles $ graph maxHeight numBalls
</code></pre>
<p>For example, we can find all 3-ball patterns with a maximum height of 4, along with their initial states, as follows:</p>
<pre><code>ghci&gt; map (state *** map height) $ validPatterns (Height 4) 3
[(7,[3,3]),(7,[3,4,2]),(7,[3,4,4,1]),(7,[3,4,4,4,0]),(7,[4,2]),(7,[4,4,1]),...
</code></pre>
<p>Finally, we can answer this question from earlier in the post: what is the complete list of valid n-ball tricks which can be inserted into a n-ball cascade?</p>
<p>A trick can be inserted into the n-ball cascade whenever its initial and final states equal the steady state 2^n^-1 of the n-ball cascade.</p>
<pre class="{.text}"><code>tricks :: Height -&gt; Int -&gt; [[Height]]
tricks maxHeight numBalls = 
  map snd 
  $ filter ((== (State (2^numBalls - 1))) . fst) 
  $ validPatterns maxHeight numBalls
</code></pre>
<p>For example, here are the tricks with maximum height 4 which can be inserted into the 3-ball cascade:</p>
<pre><code>ghci&gt; map (map height) $ tricks (Height 4) 3
[[3,3],[3,4,2],[3,4,4,1],[3,4,4,4,0],[4,2],[4,4,1],[4,4,4,0]]
</code></pre>
<h2>Addendum: Juggling Animator in Elm</h2>
<p>I originally wrote this juggling animator in Haskell using the HOpenGL library for rendering, and later ported the code to Elm.</p>
<p>First, we need to define some constants, including the pattern to throw:</p>
<pre class="{.text}"><code>-- The fraction of time spent holding the ball in the hand
pauseTime = 0.35
 
-- The number of milliseconds per beat
timePerThrow = 300
 
-- The displacement of the &quot;hands&quot; during a throw
handMovement = 0.2
 
-- The pattern to animate
pattern = [5]
</code></pre>
<p>A key function is <code>rotatePattern</code>, which is used to rotate a list of heights by a number of steps. This is used to shift a pattern after a throw occurs, or to adjust a pattern for each ball being juggled.</p>
<pre class="{.text}"><code>rotatePattern : Int -&gt; [a] -&gt; [a]
rotatePattern n xs =
  if | n &lt; length xs -&gt; drop n xs ++ take n xs
     | otherwise     -&gt; rotatePattern (n - length xs) xs
</code></pre>
<p>Given the current time (in units of beats), we can calculate the height of the current throw, and the coordinates of the current ball by repeatedly rotating the pattern:</p>
<pre class="{.text}"><code>heightAtTime : [Int] -&gt; Float -&gt; Int
heightAtTime hs t =
  let h = head hs in
  if | t &lt; toFloat h     -&gt; h
     | otherwise         -&gt; heightAtTime (rotatePattern h hs) (t - toFloat h)
 
xAtTime : [Int] -&gt; Float -&gt; Float
xAtTime hs t =
  let h = head hs in
  if | t &lt; pauseTime                       -&gt; t / pauseTime * handMovement
     | (t &lt; toFloat h) &amp;&amp; (h `mod` 2 == 0) -&gt; handMovement * (toFloat h - t) / (toFloat h - pauseTime)
     | t &lt; toFloat h                       -&gt; handMovement + (1 - handMovement) * (t - pauseTime) / (toFloat h - pauseTime)
     | h `mod` 2 == 0                      -&gt; xAtTime (rotatePattern h hs) (t - toFloat h)
     | otherwise                           -&gt; 1 + xAtTime (rotatePattern h hs) (t - toFloat h)
 
yAtTime : [Int] -&gt; Float -&gt; Float
yAtTime hs t  =
  let h = head hs in
  if | t &lt; pauseTime -&gt; 0
     | t &lt; toFloat h -&gt; (t - pauseTime) / (toFloat h - pauseTime)
     | otherwise     -&gt; yAtTime (rotatePattern h hs) (t - toFloat h)
</code></pre>
<p>The <code>x</code>-coordinate needs to be clipped to the range <code>0-1</code>. Reflections occur at integer values of <code>x</code>, since the <code>x</code> coordinate represents the total displacement along the <code>x</code>-axis.</p>
<pre class="{.text}"><code>clip : Float -&gt; Float
clip x =
  if | x &gt; 1     -&gt; 1 - clip (x - 1)
     | x &lt; 0     -&gt; clip (0 - x)
     | otherwise -&gt; x
</code></pre>
<p>We can now compute the position of each ball. I have hard-coded the width and height of the canvas to 300 pixels here, but these could be provided as parameters.</p>
<pre class="{.text}"><code>project : Float -&gt; Float -&gt; Int -&gt; Int -&gt; (Float, Float)
project x y h maxHeight = 
  let
    xProj = 280.0 * x - 140.0
    yProj = 280.0 * (4.0 * toFloat h * y * (1.0 - y) / toFloat maxHeight) - 140.0
  in
    (xProj, yProj)
    
positionOfBallAt : [Int] -&gt; Int -&gt; Float -&gt; Int -&gt; (Float, Float)
positionOfBallAt heights maxHeight time index =
  let 
    x      = xAtTime heights time
    y      = yAtTime heights time
    cx     = clip (toFloat index + x)
    h      = heightAtTime heights time
  in
    project cx y (h - 1) maxHeight
</code></pre>
<p><code>positionOfBallAt</code> gives the position of the ball, and <code>circle</code> is used to render it. The <code>frame</code> function renders each ball as a circle at its current position:</p>
<pre class="{.text}"><code>frameFor : [Int] -&gt; Int -&gt; Int -&gt; Int -&gt; Int -&gt; Form
frameFor heights timePeriod maxHeight millis index =
  let 
    startMillis     = index * timePerThrow
    relativeTime    = toFloat ((millis - startMillis) `mod` timePeriod) / toFloat timePerThrow
    rotatedPattern  = rotatePattern index heights
    position        = positionOfBallAt rotatedPattern maxHeight relativeTime index
  in 
    circle 10 |&gt; filled black |&gt; move position
 
frame : [Int] -&gt; Int -&gt; Element
frame heights millis = 
  let
    period          = 2 * sum heights
    timePeriod      = timePerThrow * period
    maxHeight       = maximum heights
  in
    [0 .. period - 1] |&gt; List.map (frameFor heights timePeriod maxHeight millis) |&gt; collage 300 300 
</code></pre>
<p><code>main</code> simply invokes <code>frame</code> to render the current scene once every millisecond (or as fast as possible):</p>
<pre class="{.text}"><code>main : Signal Element
main = lift (frame pattern . round) (every millisecond)
</code></pre>
</main><footer><hr><p class="text-muted"><small>Â© Phil Freeman 2010-2018</small></p></footer></body></html>